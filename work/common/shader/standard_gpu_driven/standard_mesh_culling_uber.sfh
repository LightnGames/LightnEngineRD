#include "../gpu_culling_function.sfh"
struct StandardIndirectArgument {
    uint _meshInstanceIndex;
    uint _materialIndex;
    uint _triangleOffset;
    uint _indexCountPerInstance;
    uint _instanceCount;
    uint _startIndexLocation;
    int _baseVertexLocation;
    uint _startInstanceLocation;
};

ConstantBuffer<SceneCullingInfo> _sceneCullingInfo : register(b0);
ConstantBuffer<ViewInfo> _viewInfo :register(b2);
StructuredBuffer<Mesh> _meshes : register(t0);
StructuredBuffer<LodMesh> _lodMeshes : register(t1);
StructuredBuffer<SubMesh> _subMeshes : register(t2);
// StructuredBuffer<Meshlet> _meshlets : register(t3);
StructuredBuffer<MeshInstance> _meshInstances : register(t4);
StructuredBuffer<LodMeshInstance> _lodMeshInstances : register(t5);
StructuredBuffer<SubMeshInstance> _subMeshInstances : register(t6);
ByteAddressBuffer _indirectArgumentOffsets : register(t7);
ByteAddressBuffer _currentLodLevels : register(t8);
StructuredBuffer<SubMeshDrawInfo> _subMeshDrawInfos : register(t9);
ByteAddressBuffer _materialInstanceIndices : register(t10);
#if UBER_OCCLUSION
Texture2D<float> _hizTextures[] : register(t13);
#endif

RWStructuredBuffer<StandardIndirectArgument> _indirectArguments : register(u0);
RWByteAddressBuffer _countBuffer : register(u1);

#if UBER_CULLING_RESULT
RWByteAddressBuffer _cullingResult : register(u5);
#endif

#define THREAD_NUM 128

bool isRangeMeshInstance(uint meshInstanceIndex) {
    return meshInstanceIndex < _sceneCullingInfo._meshInstanceCount;
}

bool isTestedMeshInstance(uint meshInstanceIndex) {
    if (!isRangeMeshInstance(meshInstanceIndex)) {
        return false;
    }
    
    MeshInstance meshInstance = _meshInstances[meshInstanceIndex];
    if (!isEnabledMeshInstance(meshInstance)) {
        return false;
    }
    
    return true;
}

Mesh getMesh(MeshInstance meshInstance) {
    Mesh mesh = _meshes[meshInstance._meshIndex];
    if (mesh._stateFlags == MESH_STATE_ALLOCATED) {
        mesh = _meshes[0];
    }
    return mesh;
}

LodMesh getLodMesh(Mesh mesh, uint lodLevel) {
    uint lodMeshIndex = mesh._lodMeshOffset + lodLevel;
    return _lodMeshes[lodMeshIndex];
}

uint getMeshInstanceIndex(uint groupId, uint groupThreadId) {
    return groupId * THREAD_NUM + groupThreadId;
}

uint getCurrentLodLevel(uint meshInstanceIndex) {
    return _currentLodLevels.Load(meshInstanceIndex * UINT32_SIZE_IN_BYTE);
}

void addIndirectArgument(uint meshInstanceIndex) {
    MeshInstance meshInstance = _meshInstances[meshInstanceIndex];
    Mesh mesh = getMesh(meshInstance);
    uint lodLevel = getCurrentLodLevel(meshInstanceIndex);
	
    LodMesh lodMesh = getLodMesh(mesh, lodLevel);
    uint subMeshCount = lodMesh._subMeshCount;
    
    LodMeshInstance lodMeshInstance = _lodMeshInstances[meshInstance._lodMeshInstanceOffset + lodLevel];
    for (uint subMeshLocalIndex = 0; subMeshLocalIndex < subMeshCount; ++subMeshLocalIndex) {
        uint subMeshInstanceIndex = lodMeshInstance._subMeshInstanceOffset + subMeshLocalIndex;
        SubMesh subMesh = _subMeshes[lodMesh._subMeshOffset + subMeshLocalIndex];
        SubMeshInstance subMeshInstance = _subMeshInstances[subMeshInstanceIndex];
        SubMeshDrawInfo subMeshDrawInfo = _subMeshDrawInfos[lodMesh._subMeshOffset + subMeshLocalIndex];
			
        uint materialInstanceOffsetByte = subMeshInstance._materialIndex * UINT32_SIZE_IN_BYTE;
        uint materialInstanceIndex = _materialInstanceIndices.Load(materialInstanceOffsetByte);
        
        uint shaderSetOffset = subMeshInstance._shaderSetIndex * UINT32_SIZE_IN_BYTE;
        uint currentCountIndex = 0;
        _countBuffer.InterlockedAdd(shaderSetOffset, 1, currentCountIndex);
	
        uint indirectArgumentShaderSetOffset = _indirectArgumentOffsets.Load(shaderSetOffset);
        uint indirectArgumentOffset = indirectArgumentShaderSetOffset + currentCountIndex;
		
        StandardIndirectArgument arguments;
        arguments._meshInstanceIndex = meshInstanceIndex;
        arguments._materialIndex = materialInstanceIndex;
        arguments._triangleOffset = subMeshDrawInfo._indexOffset / 3;
        arguments._indexCountPerInstance = subMeshDrawInfo._indexCount;
        arguments._startIndexLocation = subMeshDrawInfo._indexOffset;
        arguments._instanceCount = 1;
        arguments._startInstanceLocation = 0;
        arguments._baseVertexLocation = lodMesh._vertexOffset;
        _indirectArguments[indirectArgumentOffset] = arguments;
    }
}


[numthreads(THREAD_NUM, 1, 1)]
void main(uint gid : SV_GroupID, uint gtid : SV_GroupThreadID, uint dtid : SV_DispatchThreadID) {
    uint meshInstanceIndex = dtid;
    if (!isTestedMeshInstance(meshInstanceIndex)) {
        return;
    }
    
    MeshInstance meshInstance = _meshInstances[meshInstanceIndex];
    if (!(meshInstance._stateFlags & MESH_INSTANCE_STATE_FLAGS_VISIBLE)) {
        return;
    }
    
    Mesh mesh = getMesh(meshInstance);
    uint lodLevel = getCurrentLodLevel(meshInstanceIndex);
	
    LodMesh lodMesh = getLodMesh(mesh, lodLevel);
    uint subMeshCount = lodMesh._subMeshCount;
    
    ViewInfo viewInfo = _viewInfo;
    float3 boundsMin = meshInstance._aabbMin;
    float3 boundsMax = meshInstance._aabbMax;
    bool passCulling = true;
    bool passFrustum = false;
    bool passOcclusion = false;
    bool testFrustum = false;
    bool testOcclusion = false;
#if UBER_FRUSTUM
    passFrustum = doFrustumCulling(boundsMin, boundsMax, viewInfo);
    testFrustum = true;
    passCulling = passFrustum;
#endif
    
#if UBER_OCCLUSION
    if (passFrustum) {
        float2 screenCoordsMin;
        float2 screenCoordsMax;
        computeScreenRect(boundsMin, boundsMax, viewInfo, screenCoordsMin, screenCoordsMax);
    
        uint hizLevel;
        uint2 coords;
        uint2 sampleCount;
        uint2 hizSize;
        testOcclusion = computeHizLevelAndCoords(screenCoordsMin, screenCoordsMax, viewInfo, hizLevel, coords, sampleCount, hizSize);
        if (testOcclusion) {
            float hizDepthValue = sampleHiz(hizSize, sampleCount, coords, _hizTextures[hizLevel]);
            passOcclusion = isOcclusion(hizDepthValue, boundsMin, boundsMax, viewInfo);
            passCulling = passOcclusion;
        }
    }
#endif
    
#if UBER_CULLING_RESULT
    uint meshletCount = 0;
    uint triangleCount = 0;
    for (uint subMeshLocalIndex = 0; subMeshLocalIndex < subMeshCount; ++subMeshLocalIndex) {
        uint subMeshIndex = lodMesh._subMeshOffset + subMeshLocalIndex;
        meshletCount += _subMeshes[subMeshIndex]._meshletCount;
        triangleCount += _subMeshDrawInfos[subMeshIndex]._indexCount / 3;
    }
    
    uint currentCount = 0;
    if (testFrustum) {
        _cullingResult.InterlockedAdd(TEST_FRUSTUM_CULLING_MESH_INSTANCE_COUNT_OFFSET, 1, currentCount);
        _cullingResult.InterlockedAdd(TEST_FRUSTUM_CULLING_SUBMESH_INSTANCE_COUNT_OFFSET, subMeshCount, currentCount);
        _cullingResult.InterlockedAdd(TEST_FRUSTUM_CULLING_TRIANGLE_COUNT_OFFSET, triangleCount, currentCount);
        _cullingResult.InterlockedAdd(TEST_FRUSTUM_CULLING_MESHLET_INSTANCE_COUNT_OFFSET, meshletCount, currentCount);
    }
    
    if (testOcclusion) {
        _cullingResult.InterlockedAdd(TEST_OCCLUSION_CULLING_MESH_INSTANCE_COUNT_OFFSET, 1, currentCount);
        _cullingResult.InterlockedAdd(TEST_OCCLUSION_CULLING_SUBMESH_INSTANCE_COUNT_OFFSET, subMeshCount, currentCount);
        _cullingResult.InterlockedAdd(TEST_OCCLUSION_CULLING_TRIANGLE_COUNT_OFFSET, triangleCount, currentCount);
        _cullingResult.InterlockedAdd(TEST_OCCLUSION_CULLING_MESHLET_INSTANCE_COUNT_OFFSET, meshletCount, currentCount);
    }
    
    if (passFrustum) {
        _cullingResult.InterlockedAdd(PASSED_FRUSTUM_CULLING_MESH_INSTANCE_COUNT_OFFSET, 1, currentCount);
        _cullingResult.InterlockedAdd(PASSED_FRUSTUM_CULLING_SUBMESH_INSTANCE_COUNT_OFFSET, subMeshCount, currentCount);
        _cullingResult.InterlockedAdd(PASSED_FRUSTUM_CULLING_TRIANGLE_COUNT_OFFSET, triangleCount, currentCount);
        _cullingResult.InterlockedAdd(PASSED_FRUSTUM_CULLING_MESHLET_INSTANCE_COUNT_OFFSET, meshletCount, currentCount);
    }
    
    if (passOcclusion) {
        _cullingResult.InterlockedAdd(PASSED_OCCLUSION_CULLING_MESH_INSTANCE_COUNT_OFFSET, 1, currentCount);
        _cullingResult.InterlockedAdd(PASSED_OCCLUSION_CULLING_SUBMESH_INSTANCE_COUNT_OFFSET, subMeshCount, currentCount);
        _cullingResult.InterlockedAdd(PASSED_OCCLUSION_CULLING_TRIANGLE_COUNT_OFFSET, triangleCount, currentCount);
        _cullingResult.InterlockedAdd(PASSED_OCCLUSION_CULLING_MESHLET_INSTANCE_COUNT_OFFSET, meshletCount, currentCount);
    }
#endif
    if (passCulling) {
        addIndirectArgument(meshInstanceIndex);
    }
}