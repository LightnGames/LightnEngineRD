#include "../constant_buffer.sfh"
#include "../shaderInput.sfh"
#include "../gpu_driven.sfh"

ConstantBuffer<ViewInfo> _viewInfo : register(b0);

StructuredBuffer<CustomParameters> _customParameters : register(t0);
StructuredBuffer<Mesh> _meshes : register(t1);
StructuredBuffer<LodMesh> _lodMeshes : register(t2);
StructuredBuffer<SubMesh> _subMeshes : register(t3);
StructuredBuffer<Meshlet> _meshlets : register(t4);

StructuredBuffer<MeshInstance> _meshInstances : register(t5);
StructuredBuffer<LodMeshInstance> _lodMeshInstances : register(t6);
StructuredBuffer<SubMeshInstance> _subMeshInstances : register(t7);

StructuredBuffer<uint> _vertexIndices : register(t9);
StructuredBuffer<uint> _primitives : register(t10);
StructuredBuffer<float3> _vertexPositions : register(t11);
StructuredBuffer<uint> _vertexNormalTangents : register(t12);
StructuredBuffer<uint> _vertexTexcoords : register(t13);

ByteAddressBuffer _currentLodLevels : register(t15);

[NumThreads(128, 1, 1)]
[OutputTopology("triangle")]
void main(
    uint gtid : SV_GroupThreadID,
    uint gid : SV_GroupID,
    in payload PayloadStruct payload,
    out indices uint3 tris[126],
    out vertices VertexOut verts[64]
){
    uint meshInstanceIndex = payload._meshInstanceIndices[gid];
    uint meshletIndex = payload._meshletIndices[gid];
    MeshInstance meshInstance = _meshInstances[meshInstanceIndex];
    Meshlet meshlet = _meshlets[meshletIndex];
    Mesh mesh = _meshes[meshInstance._meshIndex];

    uint vertexCount = meshlet._vertexCount;
    uint primitiveCount = meshlet._primitiveCount;
    SetMeshOutputCounts(vertexCount, primitiveCount);

    if(gtid < primitiveCount){
        uint primitiveIndex = meshlet._primitiveOffset + gtid;
        uint packedIndex = _primitives[payload._primitiveOffset + primitiveIndex];
        tris[gtid] = uint3(packedIndex & 0xff, (packedIndex >> 8) & 0xff, (packedIndex >> 16) & 0xff);
    }

    if(gtid < vertexCount){
        uint vertexLocalIndex = meshlet._vertexOffset + gtid;
        uint vertexIndex = _vertexIndices[payload._vertexIndexOffset + vertexLocalIndex];
        uint vertexOffset = payload._vertexOffset + vertexIndex;
        float3 positionVertex = _vertexPositions[vertexOffset];

        float3 offset = float3(payload._meshInstanceOffsets[gid] * 3.0, 0, 0);
        //float3 offset = float3(0, 0, 0);
        float4 worldPos = mul(float4(positionVertex + offset, 1), meshInstance._matrixWorld);

        uint currentLodLevel = _currentLodLevels.Load(meshInstanceIndex * 4);
        
        VertexOut vertexOut;
        vertexOut._position = mul(worldPos, _viewInfo._matrixViewProj);
        vertexOut._positionNdc = worldPos;
        vertexOut._texcoord = unpackTexCoords(_vertexTexcoords[vertexOffset]);
        vertexOut._meshletIndex = meshletIndex;
        vertexOut._materialIndex = payload._materialIndex;

        vertexOut._lodLevel = 0.0;
        if(mesh._lodMeshCount > 1){
            vertexOut._lodLevel = currentLodLevel / (float)(mesh._lodMeshCount - 1);
        }

        verts[gtid] =
vertexOut;
    }
}
