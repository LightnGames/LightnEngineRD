#include "gpu_culling_function.sfh"

ByteAddressBuffer _infoOffsets : register(t0);
ByteAddressBuffer _infoCounts : register(t1);
StructuredBuffer<SubMesh> _subMeshes : register(t2);
StructuredBuffer<Meshlet> _meshlets : register(t3);

RWStructuredBuffer<DispatchMeshIndirectArgument> _indirectArguments : register(u0);
RWByteAddressBuffer _countBuffer : register(u1);

[numthreads(128, 1, 1)]
void main(uint gid : SV_GroupID, uint gtid : SV_GroupThreadID, uint dtid : SV_DispatchThreadID) {
    uint instanceCount = _infoCounts.Load(dtid * UINT32_SIZE_IN_BYTE);
    if (instanceCount == 0) {
        return;
    }
    
    uint subMeshIndex = dtid % INSTANCING_PER_SHADER_COUNT_MAX;
    uint shaderSetIndex = dtid / INSTANCING_PER_SHADER_COUNT_MAX;
    SubMesh subMesh = _subMeshes[subMeshIndex];
    uint meshletCount = subMesh._meshletCount;
    
    uint dispatchCount = (instanceCount * meshletCount / 32) + 1;
    if (meshletCount == 1) {
        Meshlet meshlet = _meshlets[subMesh._meshletOffset];
        uint instanceCountPerThreadGroup = min(64 / meshlet._vertexCount, 126 / meshlet._primitiveCount);
        dispatchCount = (instanceCount / instanceCountPerThreadGroup) + 1;
        meshletCount = subMesh._meshletOffset;
    }
    
    IndirectArgumentConstant constant;
    constant._meshletInstanceInfoOffset = _infoOffsets.Load(dtid * UINT32_SIZE_IN_BYTE);
    constant._instanceCount = instanceCount;
    constant._meshletCount = meshletCount;
    
    DispatchMeshIndirectArgument arguments;
    arguments._constant = constant;
    arguments._dispatchX = dispatchCount;
    arguments._dispatchY = 1;
    arguments._dispatchZ = 1;
    
    uint currentCount = 0;
    uint countOffset = shaderSetIndex * UINT32_SIZE_IN_BYTE;
    _countBuffer.InterlockedAdd(countOffset, 1, currentCount);
    
    uint indirectArgumentOffset = shaderSetIndex * INSTANCING_PER_SHADER_COUNT_MAX + currentCount;
    _indirectArguments[indirectArgumentOffset] = arguments;
}