#include "gpu_culling_function.sfh"

struct Constant {
    uint _packedMeshletCount;
};

ConstantBuffer<Constant> _constant : register(b0);

ByteAddressBuffer _infoOffsets : register(t0);
ByteAddressBuffer _infoCounts : register(t1);
StructuredBuffer<SubMesh> _subMeshes : register(t2);
StructuredBuffer<Meshlet> _meshlets : register(t3);
StructuredBuffer<MeshletPrimitiveInfo> _meshletPrimitiveInfos : register(t4);

RWStructuredBuffer<DispatchMeshIndirectArgument> _indirectArguments : register(u0);
RWByteAddressBuffer _countBuffer : register(u1);
RWStructuredBuffer<DispatchMeshIndirectArgument> _primIndirectArguments : register(u2);
RWByteAddressBuffer _primCountBuffer : register(u3);

[numthreads(128, 1, 1)]
void main(uint gid : SV_GroupID, uint gtid : SV_GroupThreadID, uint dtid : SV_DispatchThreadID) {
    uint instanceCount = _infoCounts.Load(dtid * UINT32_SIZE_IN_BYTE);
    if (instanceCount == 0) {
        return;
    }
    
    uint subMeshIndex = dtid % INSTANCING_PER_SHADER_COUNT_MAX;
    uint shaderSetIndex = dtid / INSTANCING_PER_SHADER_COUNT_MAX;
    SubMesh subMesh = _subMeshes[subMeshIndex];
    uint meshletCount = subMesh._meshletCount;

    MeshShaderIndirectArgument constant;
    constant._meshletInstanceInfoOffset = _infoOffsets.Load(dtid * UINT32_SIZE_IN_BYTE);
    constant._instanceCount = instanceCount;
    constant._meshletCount = meshletCount;
    constant._meshletOffset = subMesh._meshletOffset;
    
    uint currentCount = 0;
    uint packThreshold = _constant._packedMeshletCount;
    uint countOffset = shaderSetIndex * UINT32_SIZE_IN_BYTE;
    uint shaderSetOffset = shaderSetIndex * INSTANCING_PER_SHADER_COUNT_MAX;
    if (meshletCount <= packThreshold) {
        MeshletPrimitiveInfo meshlet = _meshletPrimitiveInfos[subMesh._meshletOffset + meshletCount - 1];
        uint instanceCountPerThreadGroup = min(64 / meshlet._vertexCount, 126 / meshlet._primitiveCount);
        uint baseInstanceCount = (meshletCount - 1) * instanceCount;
        uint packInstanceCount = (instanceCount / instanceCountPerThreadGroup) + 1;
        uint dispatchCount = baseInstanceCount + packInstanceCount;
        
        DispatchMeshIndirectArgument arguments;
        arguments._constant = constant;
        arguments._dispatchX = dispatchCount;
        arguments._dispatchY = 1;
        arguments._dispatchZ = 1;
        
        _primCountBuffer.InterlockedAdd(countOffset, 1, currentCount);
        _primIndirectArguments[shaderSetOffset + currentCount] = arguments;
    } else {
        uint dispatchCount = (instanceCount * meshletCount / 32) + 1;
        DispatchMeshIndirectArgument arguments;
        arguments._constant = constant;
        arguments._dispatchX = dispatchCount;
        arguments._dispatchY = 1;
        arguments._dispatchZ = 1;
        
        _countBuffer.InterlockedAdd(countOffset, 1, currentCount);
        _indirectArguments[shaderSetOffset + currentCount] = arguments;
    }
}