#define FLT_MAX 3.402823466e+38
#define EPSILON 0.000001
#define UINT16_SIZE_IN_BYTE 2
#define UINT32_SIZE_IN_BYTE 4
#define UINT8_MAX 255
#define UINT16_MAX 65535
#define UINT32_MAX 4294967295
#define POS_NAN 0x7f800000

float3 heatMap(float unormValue)
{
    float redValue = min(unormValue * 2.0, 1.0);
    float blueValue = max((unormValue - 0.5) * 2.0, 0.0);
    return lerp(float3(1, 0, 0), lerp(float3(0, 1, 0), float3(0, 0, 1), blueValue), redValue);
}

float3 uniqueColor(uint index)
{
    return float3(
            float(index & 1),
            float(index & 3) / 4,
            float(index & 7) / 8);
}

// 32bit uint -> 16bit float2
float2 unpackTexCoords(uint packed)
{
    uint x = (packed.x >> 0) & 0xffff;
    uint y = (packed.x >> 16) & 0xffff;
    float div = UINT16_MAX / 8.0;
    return (float2(x, y) / div) - 4.0;
}

float3 decodeOctahedron(uint packedVector)
{  
    float2 f = float2(packedVector & 0xff, (packedVector >> 8) & 0xff) / float2(UINT8_MAX, UINT8_MAX);
    f = f * 2.0 - 1.0;

// https://twitter.com/Stubbesaurus/status/937994790553227264
    float3 n = float3(f.x, f.y, 1.0 - abs(f.x) - abs(f.y));
    float t = saturate(-n.z);
    n.xy += n.xy >= 0.0 ? -t : t;
    return normalize(n);
}

// Values used to linearize the Z buffer (http://www.humus.name/temp/Linearize%20depth.txt)
float computeLinerDepth(float nonLinerDepth, float nearClip, float farClip)
{
    float farNear = farClip / nearClip;
    float invFarNear = 1.0 - farNear;
    return 1.0 / (invFarNear * nonLinerDepth + farNear);
}

float3 reconstructWorldPositionFromDepth(
    in float2 screenTextureCoord,
    in float depth,
    in float4x4 inverseViewProjection)
{
    float2 clipSpaceCoord = screenTextureCoord.xy * 2.0 - 1.0;
    float4 projectedPosition = float4(clipSpaceCoord, depth, 1.0);
    projectedPosition.y = -projectedPosition.y;

    float4 position = mul(projectedPosition, inverseViewProjection);
    return position.xyz / position.w;
}